# generated from
# ament_cmake_export_jni_libraries/cmake/template/ament_cmake_export_jni_libraries.cmake.in

set(_exported_jni_libraries "@_AMENT_CMAKE_EXPORT_JNI_LIBRARIES@")

# append absolute JNI libraries to @PROJECT_NAME@_JNI_LIBRARIES
# warn about not existing paths
if(NOT _exported_jni_libraries STREQUAL "")
  find_package(ament_cmake_core QUIET REQUIRED)
  foreach(_exported_jni_library ${_exported_jni_libraries})
    if(NOT IS_ABSOLUTE "${_exported_jni_library}")
      set(_jni_lib "NOTFOUND")
      find_library(
        _jni_lib NAMES "${_exported_jni_library}"
        PATHS "${@PROJECT_NAME@_DIR}/../../../lib/jni"
        NO_DEFAULT_PATH NO_CMAKE_FIND_ROOT_PATH
      )

      if(NOT _jni_lib)
        # warn about not existing library and ignore it
        message(FATAL_ERROR "Package '@PROJECT_NAME@' exports the JNI library '${_exported_jni_library}' which couldn't be found")
      elseif(NOT IS_ABSOLUTE "${_jni_lib}")
        # the found library must be an absolute path
        message(FATAL_ERROR "Package '@PROJECT_NAME@' found the JNI library '${_exported_jni_library}' at '${_jni_lib}' which is not an absolute path")
      elseif(NOT EXISTS "${_jni_lib}")
        # the found library must exist
        message(FATAL_ERROR "Package '@PROJECT_NAME@' found the JNI library '${_jni_lib}' which doesn't exist")
      else()
        list(APPEND @PROJECT_NAME@_JNI_LIBRARIES "${_jni_lib}")
      endif()

    else()
      if(NOT EXISTS "${_exported_jni_library}")
        message(WARNING "Package '@PROJECT_NAME@' exports the JNI library '${_exported_jni_library}' which doesn't exist")
      else()
        list(APPEND @PROJECT_NAME@_JNI_LIBRARIES "${_exported_jni_library}")
      endif()

    endif()
  endforeach()
endif()
